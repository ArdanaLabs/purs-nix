{ inputs =
    { get-flake.url = "github:ursi/get-flake";

      purs-nix-test-packages =
        { flake = false;
          url = "github:ursi/purs-nix-test-packages";
        };
    };

  outputs = { get-flake, purs-nix-test-packages, ... }@inputs:
    with builtins;
    let purs-nix = get-flake ../.; in
    purs-nix.inputs.utils.apply-systems
      { inputs =
          inputs
          // { inherit purs-nix;
               inherit (purs-nix.inputs) make-shell nixpkgs;
             };

        systems = [ "x86_64-linux" ];
      }
      ({ make-shell, pkgs, purs-nix, ... }:
         let
           p = pkgs;
           inherit (purs-nix) ps-pkgs;
           package = import ./package.nix purs-nix-test-packages purs-nix;
           easy-ps = import (get-flake ../.).inputs.easy-ps { inherit pkgs; };

           ps-custom = { nodejs ? null, purescript ? null }:
             purs-nix.purs
               ({ inherit (package) dependencies;
                  test-dependencies = [ ps-pkgs."assert" ];
                  srcs = [ ./src ./src2 ];
                }
                // (if isNull nodejs then {} else { inherit nodejs; })
                // (if isNull purescript then {} else { inherit purescript; })
               );

           ps = ps-custom {};

           make-app = module: name:
             { type = "app";

               program =
                 (p.writeScript name
                    ''
                    ${ps.modules.${module}.app { inherit name; }}/bin/${name};
                    ''
                 ).outPath;
             };

           make-script-custom = args: module:
             "${(ps-custom args).modules.${module}.app { name = "a"; }}/bin/a";

           make-script = make-script-custom {};
         in
         { apps.default = make-app "Main" "test";

           checks =
             mapAttrs
               (n: v: p.runCommand n {} "${v}\ntouch $out")
               { "compiler flags" =
                   let
                     output =
                       ps.modules.Main.output
                         { codegen = "corefn,js";
                           comments = true;
                           no-prefix = true;
                         };
                   in
                   ''
                   # codegen
                   ls ${output}/Main/corefn.json || exit

                   # comments
                   [[ $(grep "// a comment for testing purposes" ${output}/Main/index.js) ]] \
                   || exit

                   # no-prefix
                   [[ -z $(head -n 1 ${output}/Main/index.js | grep //) ]] || exit
                   '';

               "custom node package" =
                   let
                     bin = make-script-custom { inherit nodejs; } "Node";
                     nodejs = p.nodejs-14_x;
                   in
                   ''
                   [[ $(${bin}) == v${nodejs.version} ]] || exit
                   '';

                 "custom purescript package" =
                   let
                     output =
                         (ps-custom { inherit purescript; }).modules.Main.output {};

                     purescript = easy-ps.purs-0_15_0;
                   in
                   ''
                   [[ $(head -n 1 ${output}/Main/index.js) \
                   == "// Generated by purs version ${substring 1 99 purescript.version}" \
                   ]] || exit
                   '';

                 "main output" =
                   ''
                   output=$(${make-script "Main"})

                   target="prelude override
                   effect override
                   2 is even
                   3 isn't even
                   ‚ùÑ"

                   [[ $output == $target ]] || exit
                   '';
               };

           devShells.default =
             make-shell
               { packages =
                   with pkgs;
                   [ nodejs

                     (ps.command
                        { name = "purs-nix-test";
                          inherit package;
                          srcs = [ "src" "src2" ];
                        }
                     )

                     purs-nix.esbuild
                     purs-nix.purescript
                     purs-nix.purescript-language-server
                   ];
               };
         }
      );
}
